# - CMAKE Config file for the TiledArray package
# The following variables are defined:
#  TILEDARRAY_FOUND                - System has the TiledArray package
#  TILEDARRAY_INCLUDE_DIRS         - The TiledArray include directory
#  TILEDARRAY_LIBRARIES            - The TiledArray libraries and their dependencies
#  TILEDARRAY_VERSION              - The TiledArray (core) version; see semver.org
#  TILEDARRAY_EXT_VERSION          - The TiledArray version, includes prerelease id; see semver.org

# Set package version
set(TILEDARRAY_VERSION "@TILEDARRAY_VERSION@")
set(TILEDARRAY_EXT_VERSION "@TILEDARRAY_EXT_VERSION@")

@PACKAGE_INIT@

# Include library IMPORT targets
if(NOT TARGET tiledarray)
  include("${CMAKE_CURRENT_LIST_DIR}/tiledarray-targets.cmake")
endif()
if(NOT TARGET MADworld)
  # if madness installed separately, use the madness install discovered when configuring TA
  set(MADNESS_CONFIG_DIR "@MADNESS_CONFIG_DIR@")
  # otherwise assume TA built madness
  if (MADNESS_CONFIG_DIR STREQUAL "")
    set(MADNESS_CONFIG_DIR "${CMAKE_CURRENT_LIST_DIR}/../madness")
  endif()
  find_package(MADNESS 0.10.1 CONFIG QUIET REQUIRED COMPONENTS world PATHS ${MADNESS_CONFIG_DIR} NO_DEFAULT_PATH)
endif()

# if TA is a CUDA-dependent library it needs CUDA to link properly ... unfortunately CMake is not able to do this correctly
# see https://gitlab.kitware.com/cmake/cmake/issues/18614
# so try workarounds
set(TILEDARRAY_HAS_CUDA "@CUDA_FOUND@")
if(TILEDARRAY_HAS_CUDA)
  set(CMAKE_CUDA_HOST_COMPILER "@CMAKE_CUDA_HOST_COMPILER@")
  set(CUDA_LIBRARIES "@CUDA_LIBRARIES@")
  get_target_property(_ta_interface_libs tiledarray INTERFACE_LINK_LIBRARIES)
  if(_ta_interface_libs)
    list(APPEND _ta_interface_libs ${CUDA_LIBRARIES})
  else()
    set(_ta_interface_libs ${CUDA_LIBRARIES})
  endif()
  set_target_properties(tiledarray PROPERTIES
    INTERFACE_LINK_LIBRARIES "${_ta_interface_libs}")
endif()

# Set the tiledarray compiled library target
set(TILEDARRAY_LIBRARIES tiledarray)

set(TILEDARRAY_SOURCE_DIR "@TILEDARRAY_SOURCE_DIR@")
set(TILEDARRAY_BINARY_DIR "@TILEDARRAY_BINARY_DIR@")

set(TILEDARRAY_BUILD_INCLUDE_DIRS "${TILEDARRAY_SOURCE_DIR}/src" "${TILEDARRAY_BINARY_DIR}/src")
set(TILEDARRAY_INSTALL_INCLUDE_DIRS "@PACKAGE_TILEDARRAY_INSTALL_INCLUDEDIR@"
      "@PACKAGE_TILEDARRAY_INSTALL_INCLUDEDIR@/TiledArray/external")

# define TILEDARRAY_INCLUDE_DIRS according to where we are compiling: TA build tree or outside
# external packages should use TILEDARRAY_BUILD_INCLUDE_DIRS and TILEDARRAY_INSTALL_INCLUDE_DIRS directly
if(CMAKE_CURRENT_LIST_DIR EQUAL TILEDARRAY_BINARY_DIR)
  set(TILEDARRAY_INCLUDE_DIRS "${TILEDARRAY_BUILD_INCLUDE_DIRS}")
else()
  set(TILEDARRAY_INCLUDE_DIRS "${TILEDARRAY_INSTALL_INCLUDE_DIRS}")
endif()

set(TILEDARRAY_CMAKE_TOOLCHAIN_FILE "@CMAKE_TOOLCHAIN_FILE@")

set(TILEDARRAY_FOUND TRUE)
