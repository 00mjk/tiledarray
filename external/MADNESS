# -*- mode: cmake -*-

###################
# Find MADNESS
###################

find_package(Madness COMPONENTS MADworld)

if(MADNESS_FOUND)

  # sanity check: try compiling a simple program
  list(APPEND CMAKE_REQUIRED_INCLUDES ${MADNESS_INCLUDE_DIRS})
  list(APPEND CMAKE_REQUIRED_LIBRARIES ${MADNESS_LIBRARIES})
  CHECK_CXX_SOURCE_COMPILES(
    "
    #include <world/world.h>
    int main(int argc, char** argv) {
      madness::World& world = madness::initialize(argc, argv);
      return 0;
    }
    "  MADNESS_COMPILES)
  if (NOT MADNESS_COMPILES)
    message(FATAL_ERROR "MADNESS found at ${MADNESS_INCLUDE_DIR}, but could not compile test program")
  endif()

elseif(TA_EXPERT)

  message("** MADNESS was not found or explicitly set")
  message(FATAL_ERROR "** Downloading and building MADNESS is explicitly disabled in EXPERT mode")

else()

  include(ExternalProject)

  # Set paths for MADNESS project
  set(EXTERNAL_SOURCE_DIR ${PROJECT_SOURCE_DIR}/external/src/madness)
  set(EXTERNAL_BUILD_DIR  ${PROJECT_BINARY_DIR}/external/build/madness)
  set(MADNESS_SVN_URL http://m-a-d-n-e-s-s.googlecode.com/svn/local/trunk)

  message(STATUS "MADNESS not found")
  message("** Will pull MADNESS from ${MADNESS_SVN_URL}")  
    
  # Convert LAPACK_LIBRARIES to compiler arguments for MADNESS configure script
  include(ConvertLibrariesListToCompilerArgs)
  convert_libs_to_compargs(LAPACK_LIBS "${LAPACK_LIBRARIES}")
  
  if(NOT CMAKE_MAKE_PROGRAM)
    message(FATAL_ERROR "CMAKE_MAKE_PROGRAM not set. The MADNESS build system requires a GNU complient make program.")
  endif()
  
  if(MPI_FOUND)
    set(MAD_STUB_MPI "no")
  else()
    set(MAD_STUB_MPI "yes")
  endif()
  
  ExternalProject_Add(madness
    PREFIX ${CMAKE_INSTALL_PREFIX}
    STAMP_DIR ${EXTERNAL_BUILD_DIR}/stamp
   #--Download step--------------
    SVN_REPOSITORY ${MADNESS_SVN_URL}
    SVN_TRUST_CERT 1
   #--Update/Patch step----------
    UPDATE_COMMAND ""
    PATCH_COMMAND aclocal -I ./ && autoconf && autoheader && automake --add-missing
   #--Configure step-------------
    SOURCE_DIR ${EXTERNAL_SOURCE_DIR}
    CONFIGURE_COMMAND ${EXTERNAL_SOURCE_DIR}/configure
      --quiet
      --prefix=${CMAKE_INSTALL_PREFIX}
      --with-mpi-thread=multiple
      --disable-optimal
      --enable-dependency-tracking
      --with-fortran-int32=${MAD_F77_INT32}
      --with-stubmpi=${MAD_STUB_MPI}
      MPICXX=${CMAKE_CXX_COMPILER}
      MPICC=${CMAKE_C_COMPILER}
      CPPFLAGS=${CMAKE_CPP_FLAGS}
      CXX=${CMAKE_CXX_COMPILER} CXXFLAGS=${CMAKE_CXX_FLAGS}
      CC=${CMAKE_C_COMPILER} CFLAGS=${CMAKE_C_FLAGS}
      F77=${CMAKE_Fortran_COMPILER} FFLAGS=${CMAKE_Fortran_FLAGS}
      LDFLAGS=${CMAKE_EXE_LINKER_FLAGS} LIBS=${LAPACK_LIBS}
   #--Build step-----------------
    BINARY_DIR ${EXTERNAL_BUILD_DIR}
    BUILD_COMMAND ${CMAKE_MAKE_PROGRAM} libraries V=0
   #--Install step---------------
    INSTALL_COMMAND ""
    STEP_TARGETS download patch configure build
    )

  # Add madness-update target that will pull updates to the madness source from
  # the svn repository. This is done outside ExternalProject_add to prevent
  # madness from doing a full pull, configure, and build everytime the project
  # is built.
  add_custom_target(madness-update
    COMMAND ${Subversion_SVN_EXECUTABLE} update -r HEAD --force
    COMMAND ${CMAKE_COMMAND} -E touch_nocreate ${EXTERNAL_BUILD_DIR}/stamp/madness-configure
    WORKING_DIRECTORY ${EXTERNAL_SOURCE_DIR}
    COMMENT Updating source for 'madness' from the SVN repository)

  # Add madness-clean target that will delete files generated by MADNESS build.
  add_custom_target(madness-clean
    COMMAND ${CMAKE_MAKE_PROGRAM} clean
    COMMAND ${CMAKE_COMMAND} -E touch_nocreate ${EXTERNAL_BUILD_DIR}/stamp/madness-configure
    WORKING_DIRECTORY ${EXTERNAL_BUILD_DIR}
    COMMENT Cleaning build directory for 'madness')
  
  # Since 'madness-install' target cannot be linked to the 'install' target,
  # we will do it manually here. Hopefully this does not break in the future.
  install(
    DIRECTORY
        ${EXTERNAL_SOURCE_DIR}/include/
        ${EXTERNAL_BUILD_DIR}/include/
        ${EXTERNAL_SOURCE_DIR}/src/lib/
        ${EXTERNAL_BUILD_DIR}/src/lib/
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT madness
    FILES_MATCHING PATTERN "*.h"
    PATTERN ".deps" EXCLUDE
    )

  install(
    DIRECTORY
        ${EXTERNAL_BUILD_DIR}/src/lib/linalg/
        ${EXTERNAL_BUILD_DIR}/src/lib/misc/
        ${EXTERNAL_BUILD_DIR}/src/lib/muParser/
        ${EXTERNAL_BUILD_DIR}/src/lib/tensor/
        ${EXTERNAL_BUILD_DIR}/src/lib/tinyxml/
        ${EXTERNAL_BUILD_DIR}/src/lib/world/
    DESTINATION ${CMAKE_INSTALL_LIBDIR}
    COMPONENT madness
    FILES_MATCHING PATTERN "*MADlinalg*"
    PATTERN "*MADmisc*"
    PATTERN "*MADmuparser*"
    PATTERN "*MADtensor*"
    PATTERN "*MADtinyxml*"
    PATTERN "*MADworld*"
    PATTERN ".deps" EXCLUDE
    )

  # Set build dependencies and compiler arguments
  add_dependencies(External madness)
  set(MADNESS_LIBRARIES)
  list(APPEND MADNESS_LIBRARIES ${EXTERNAL_BUILD_DIR}/src/lib/world/libMADworld.a)
  list(APPEND MADNESS_LIBRARIES ${LAPACK_LIBRARIES})
  set(MADNESS_INCLUDE_DIR 
      ${EXTERNAL_BUILD_DIR}/include
      ${EXTERNAL_SOURCE_DIR}/include
      ${EXTERNAL_BUILD_DIR}/src/lib
      ${EXTERNAL_SOURCE_DIR}/src/lib)
    
endif()

include_directories(${MADNESS_INCLUDE_DIR})
list(APPEND LINK_LIBRARIES ${MADNESS_LIBRARIES})

message(STATUS "\tMADNESS include dir: ${MADNESS_INCLUDE_DIR}")
message(STATUS "\tMADNESS libraries: ${MADNESS_LIBRARIES}")
